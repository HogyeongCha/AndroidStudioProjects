plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services' version '4.4.2'
}

// local.properties에서 API 키 읽기 (안전한 로딩)
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}
// 키가 없으면 빈 문자열, 있으면 가져오기 (따옴표 제거 후 다시 감싸기 - 안전장치)
def geminiApiKey = localProperties.getProperty('GEMINI_API_KEY') ?: ""
geminiApiKey = geminiApiKey.replace("\"", "") // 혹시 모를 중복 따옴표 제거

android {
    namespace 'com.hyu_tech_academic_fest_25.straightup'
    compileSdk 34

    defaultConfig {
        applicationId "com.hyu_tech_academic_fest_25.straightup"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

        // [핵심] BuildConfig에 안전하게 String 타입으로 주입
        // 결과적으로 Java 코드에 public static final String GEMINI_API_KEY = "키값"; 형태로 생성됨
        buildConfigField "String", "GEMINI_API_KEY", "\"${geminiApiKey}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    aaptOptions {
        noCompress "tflite", "task"
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy {
        force 'org.tensorflow:tensorflow-lite:2.17.0'
        force 'org.tensorflow:tensorflow-lite-gpu:2.17.0'
        force 'org.tensorflow:tensorflow-lite-api:2.17.0'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.13.1'

    implementation 'androidx.navigation:navigation-fragment:2.7.7'
    implementation 'androidx.navigation:navigation-ui:2.7.7'

    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.8.4'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.8.4'

    implementation platform('com.google.firebase:firebase-bom:33.1.2')
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-database'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    def camerax_version = "1.3.4"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"

    implementation 'com.google.ai.client.generativeai:generativeai:0.9.0'

    implementation 'com.google.mediapipe:tasks-vision:0.10.14'

    implementation ('org.tensorflow:tensorflow-lite-support:0.4.4') {
        exclude group: 'org.tensorflow', module: 'tensorflow-lite'
        exclude group: 'org.tensorflow', module: 'tensorflow-lite-api'
    }

    implementation 'org.tensorflow:tensorflow-lite:2.17.0'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.17.0'
    implementation 'org.tensorflow:tensorflow-lite-api:2.17.0'

    implementation("com.google.guava:guava:31.1-android")
    implementation("org.reactivestreams:reactive-streams:1.0.4")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}